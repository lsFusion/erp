MODULE Transport;

REQUIRE System,
        Utils,
        Employee,
        LegalEntity,
        Stock;

// Модели машины
CLASS TruckModel 'Модель';
TABLE truckModel (TruckModel);

name 'Наименование' = DATA ISTRING[50](TruckModel);

FORM truckModel 'Модель машины'
    OBJECTS m=TruckModel PANEL
    PROPERTIES (m) name

    EDIT TruckModel OBJECT m
;

FORM truckModels 'Модели машин'
    OBJECTS m=TruckModel
    PROPERTIES(m) READONLY name
    PROPERTIES(m) NEWSESSION NEW, EDIT, DELETE 

    LIST TruckModel OBJECT m
;

// Машины
CLASS Truck 'Машина';
TABLE truck(Truck);

sid 'Номер' = DATA ISTRING[20](Truck);
truckModel = DATA TruckModel(Truck);
nameTruckModel 'Марка' (Truck truck) = name(truckModel(truck));

name 'Наименование' (Truck truck) = ISTRING[70](CONCAT ',', nameTruckModel(truck), 'г.н. '+sid(truck));

trailer 'Прицеп' = DATA ISTRING[100](Truck);
weight 'Грузоподъёмность (кг)' = DATA NUMERIC[16,4](Truck);
owner 'Владелец' = DATA ISTRING[200](Truck);
notOwnertruck = NOT owner(Truck truck);
legalEntity = DATA LegalEntity (Truck);
nameLegalEntity 'Владелец(Организация)' = fullName(legalEntity(Truck truck)); 

inactive 'Неактивный' = DATA BOOLEAN (Truck);
active 'Активный' (Truck t) =  t IS Truck AND NOT inactive(t);

FORM truck 'Машина'
    OBJECTS t=Truck PANEL
    PROPERTIES (t) sid, nameTruckModel, name, trailer, owner, weight, 
                   nameLegalEntity SHOWIF notOwnertruck(t), inactive

    EDIT Truck OBJECT t
;

DESIGN truck {
    GROUP(,t) {
        columns = 3;
    }
}

FORM trucks 'Машины'
    OBJECTS t=Truck
    PROPERTIES(t) READONLYIF isReadonly() sid, nameTruckModel, name, trailer, owner, 
                           nameLegalEntity, weight, inactive
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE 
    FILTERGROUP active FILTER 'Активные' active(t) 'F5'  DEFAULT
    LIST Truck OBJECT t
;
@extendFormEditable(trucks);

// Водители

CLASS Driver 'Водитель': Employee;

sid 'Табельный номер' = DATA BPSTRING[10](Driver);
type 'Класс' = DATA BPSTRING[10](Driver);

defaultTruck = DATA Truck(Driver);
nameDefaultTruck 'Машина по умолчанию' = name(defaultTruck(Driver driver));


FORM driver 'Водитель'
    OBJECTS e=Driver PANEL
    PROPERTIES(e)      lastName, firstName, initialsEmployee, id SHOWIF showIDs(), namePosition,
                       sid, type, nameDefaultTruck, inactive//nameMainRoleUser

    EDIT Driver OBJECT e
;

DESIGN driver {
    BOX {
        size = (1024, 768);
        NEW header {
            type = CONTAINERV;
            NEW personal {
                caption = 'Личные данные';
                type = COLUMNS;
                columns=5;                
                MOVE PROPERTY(lastName(e));
                MOVE PROPERTY(firstName(e));   
                MOVE PROPERTY(initialsEmployee(e));                  
                MOVE PROPERTY(id(e));
                MOVE PROPERTY(namePosition(e));
                MOVE PROPERTY(inactive(e));
            }
            NEW driver {
                caption = 'Данные водителя';
                type = CONTAINERH;
                MOVE PROPERTY(nameDefaultTruck(e));
                MOVE PROPERTY(sid(e));
                MOVE PROPERTY(type(e));
            }
        }
        MOVE TOOLBARBOX;
    }
}
FORM drivers 'Водители'
    PROPERTIES isEditable()
    OBJECTS e=Driver
    PROPERTIES(e) READONLYIF isReadonly() lastName, firstName, initialsEmployee, id SHOWIF showIDs(), 
                  namePosition, shortName, sid, type, nameDefaultTruck, inactive//nameMainRoleUser
    PROPERTIES(e) NEWSESSION NEW, EDIT, DELETE 
    LIST Driver OBJECT e
    FILTERGROUP inactive FILTER 'Активные' active(e) 'F5' DEFAULT
;

DESIGN drivers {
    MOVE TOOLBARBOX{
        MOVE PROPERTY(isEditable()) BEFORE TOOLBARRIGHT;
    }
}

NAVIGATOR {
    NEW FOLDER transportNavigator 'Транспорт' BEFORE administration WINDOW toolbar IMAGE 'transport.png' {
        NEW FOLDER transportDocuments 'Документы';
        NEW FOLDER transportMasterData 'Справочники' {            
            NEW trucks;
            NEW truckModels;
            NEW drivers;
        }
    }
}