MODULE SaleInvoiceRu;

REQUIRE SaleInvoice, CurrencyRu;

NAMESPACE Sale;

FORM printInvoiceToCustomerRu 'Счет покупателю'
    OBJECTS i = Invoice PANEL SUBREPORT 
    PROPERTIES nameCustomer(i), nameSupplier(i), number(i), date(i), invoiceSumInvoiceDetail(i), VATSumInvoiceDetail(i),
               nameCustomUserChief = nameCustomUserChief(customer(i)), nameCustomUserBooker = nameCustomUserBooker(customer(i)),
               addressCustomer(i), phoneCustomer(i)
    
    OBJECTS d = InvoiceDetail
    PROPERTIES(d) index, nameSku, price, valueVAT, quantity, shortNameUOMSku, invoiceSum
    FILTERS invoice(d) == i
;

printInvoiceToCustomerRu 'Счет покупателю (xls)' (Invoice i)  { 
    PRINT printInvoiceToCustomerRu OBJECTS i = i XLS;
} IMAGE 'print.png' IN print;

EXTEND FORM invoices  
    PROPERTIES(i)  printInvoiceToCustomerRu
;

DESIGN invoices {
    printContainer {
        MOVE PROPERTY (printInvoiceToCustomerRu(i));
    }
}

FORM printConsignmentARu 'Товарно-транспортная накладная (сторона А)'
    OBJECTS i = Invoice PANEL SUBREPORT
    PROPERTIES(i) includeOrders, nameSupplier, addressSupplier, nameCustomer, addressCustomer, number, date
;

printConsignmentARu 'Товарно-транспортная накладная (сторона А) (xls)' (Invoice i)  { 
    PRINT printConsignmentARu OBJECTS i = i XLS;
} IMAGE 'print.png' IN print;

EXTEND FORM invoices 
    PROPERTIES(i) printConsignmentARu
;

DESIGN invoices {
    printContainer {
        MOVE PROPERTY (printConsignmentARu(i));
    }
}

FORM printConsignmentBRu 'Товарно-транспортная накладная (сторона Б)'
    OBJECTS i = Invoice PANEL SUBREPORT
    PROPERTIES(i) includeOrders
;

printConsignmentBRu 'Товарно-транспортная накладная (сторона Б) (xls)' (Invoice i)  { 
    PRINT printConsignmentBRu OBJECTS i = i XLS;
} IMAGE 'print.png' IN print;

EXTEND FORM invoices 
    PROPERTIES(i) printConsignmentBRu
;

DESIGN invoices {
    printContainer {
        MOVE PROPERTY (printConsignmentBRu(i));
    }
}

printConsignmentABRu 'Товарно-транспортная накладная (сторона А/Б) (xls)' (Invoice i)  { 
    PRINT printConsignmentARu OBJECTS i = i XLS;
    PRINT printConsignmentBRu OBJECTS i = i XLS;
} IMAGE 'print.png' IN print;

EXTEND FORM invoices 
    PROPERTIES(i) printConsignmentABRu
;

DESIGN invoices {
    printContainer {
        MOVE PROPERTY (printConsignmentABRu(i));
    }
}

FORM printConsignment12Ru 'Торг-12'
    OBJECTS i = Invoice PANEL SUBREPORT
    PROPERTIES(i) nameSupplier, nameCustomer, number, date, quantityInvoiceDetail, sumInvoiceDetail, VATSumInvoiceDetail,
                  invoiceSumInvoiceDetail, countInvoiceDetail
    
    OBJECTS d = InvoiceDetail
    PROPERTIES(d) index, nameSku, idBarcodeSku, shortNameUOMSku, quantity, price, sum, valueVAT, VATSum, invoiceSum
    FILTERS invoice(d) == i
;

printConsignment12Ru 'Торг-12 (xls)' (Invoice i)  { 
    PRINT printConsignment12Ru OBJECTS i = i XLS;
} IMAGE 'print.png' IN print;

EXTEND FORM invoices 
    PROPERTIES(i) printConsignment12Ru
;

DESIGN invoices {
    printContainer {
        MOVE PROPERTY (printConsignment12Ru(i));
    }
}

FORM printUpdNewRu 'Универсальный передаточный документ'
    OBJECTS i = Invoice PANEL SUBREPORT
    PROPERTIES number(i), date(i), nameSupplier(i), addressSupplier(i), phoneSupplier(i), nameCustomer(i),
               addressCustomer(i), phoneCustomer(i), nameCurrency(i), extraSID = extraSID(currency(i)),
               sumInvoiceDetail(i), VATSumInvoiceDetail(i), invoiceSumInvoiceDetail(i),
               day = extractDay(date(i)), month = extractMonthName(date(i)), year = extractYear(date(i))
    
    OBJECTS d = InvoiceDetail
    PROPERTIES(d) index, idSku, nameSku, shortNameUOMSku, quantity, price, sum, valueVAT, VATSum, invoiceSum
    FILTERS invoice(d) == i
;

printUpdNewRu 'Универсальный передаточный документ (xls)' (Invoice i)  { 
    PRINT printUpdNewRu OBJECTS i = i XLS;
} IMAGE 'print.png' IN print;

EXTEND FORM invoices 
    PROPERTIES(i) printUpdNewRu
;

DESIGN invoices {
    printContainer {
        MOVE PROPERTY (printUpdNewRu(i));
    }
}