MODULE ConsignmentRu;

REQUIRE System,
        LegalEntityRu,
        Stock,
        Sku,
        Barcode,
        Employee,
        Utils,
        Numerator,
        StockContract,
        SkuLedger,
        ItemPack,
        Barcode, Employee, ItemDescription, Transport,
        PurchaseOrder;

NAMESPACE ConsignmentRu;


CLASS ABSTRACT Consignment 'Накладная';

CLASS ABSTRACT ConsignmentDetail 'Строка накладной';
               
dataDate 'Дата' (consignment) = ABSTRACT DATE (Consignment);
overDate 'Дата' (consignment) = ABSTRACT DATE (Consignment);
date 'Дата' (Consignment consignment) = OVERRIDE overDate(consignment), dataDate(consignment);

number 'Номер' = ABSTRACT STRING[28] (Consignment) IN numbered CHARWIDTH 7;
series 'Серия' = ABSTRACT BPSTRING[2] (Consignment) IN numbered CHARWIDTH 3 NOFLEX; 

dataSupplier = ABSTRACT LegalEntity (Consignment);
overSupplier = ABSTRACT LegalEntity (Consignment);
supplier (Consignment consignment)= OVERRIDE overSupplier(consignment), dataSupplier(consignment);

overINNSupplier 'ИНН получателя' = ABSTRACT STRING[50] (Consignment);
extraINNSupplier 'ИНН получателя' = ABSTRACT STRING[50] (Consignment);
INNSupplier 'ИНН отправителя' (Consignment consignment) = OVERRIDE overINNSupplier(consignment), extraINNSupplier(consignment), inn(supplier(consignment));

overKPPSupplier 'КПП получателя' = ABSTRACT STRING[50] (Consignment);
extraKPPSupplier 'КПП получателя' = ABSTRACT STRING[50] (Consignment);
KPPSupplier 'КПП отправителя' (Consignment consignment) = OVERRIDE overKPPSupplier(consignment), extraKPPSupplier(consignment), kpp(supplier(consignment));

overOKPOSupplier 'ОКПО получателя' = ABSTRACT STRING[50] (Consignment);
extraOKPOSupplier 'ОКПО получателя' = ABSTRACT STRING[50] (Consignment);
OKPOSupplier 'ОКПО отправителя' (Consignment consignment) = OVERRIDE overOKPOSupplier(consignment), extraOKPOSupplier(consignment), okpo(supplier(consignment));

overAddressSupplier = ABSTRACT ISTRING[150] (Consignment);
addressSupplier 'Юр. адрес отправителя' (Consignment consignment) =
    OVERRIDE overAddressSupplier(consignment), address(supplier(consignment), date(consignment)) CHARWIDTH 30;
    
overFullNameSupplier = ABSTRACT ISTRING[200] (Consignment);  
fullNameSupplier 'Наим-ие отправителя' (Consignment consignment) = 
    OVERRIDE overFullNameSupplier(consignment), fullName(supplier(consignment)) CHARWIDTH 30;    
            
overEmailSupplier = ABSTRACT STRING[400] (Consignment);    
emailSupplier 'E-mail' (Consignment consignment) = 
    OVERRIDE overEmailSupplier(consignment), email(supplier(consignment)) CHARWIDTH 30;
    
overSiteSupplier = ABSTRACT STRING[100] (Consignment);    
siteSupplier 'Сайт'  (Consignment consignment) = 
    OVERRIDE overSiteSupplier(consignment), site(supplier(consignment)) CHARWIDTH 30;    

overPhoneSupplier = ABSTRACT STRING[100] (Consignment);    
phoneSupplier 'Телефон/факс'  (Consignment consignment) = 
    OVERRIDE overPhoneSupplier(consignment), phone(supplier(consignment)) CHARWIDTH 30;    

dataSupplierStock (consignment) = ABSTRACT Stock (Consignment);
overSupplierStock (consignment)= ABSTRACT Stock (Consignment);
supplierStock (Consignment consignment)= 
    OVERRIDE overSupplierStock(consignment), dataSupplierStock(consignment) CHARWIDTH 20;
    
overNameSupplierStock = ABSTRACT ISTRING[150] (Consignment);
nameSupplierStock 'Склад погрузки' (Consignment consignment) =
     OVERRIDE overNameSupplierStock(consignment), name(supplierStock(consignment)) CHARWIDTH 20;
    
FORM dialogEmployeesConsignment 'Сотрудники'
    OBJECTS e = Employee
    PROPERTIES (e) READONLY inactive, lastName, firstName, namePosition, nameMainRole, userRoles
    
    FILTERGROUP active FILTER 'Активные' active(e) 'F5'  DEFAULT 
    
    LIST Employee OBJECT e
;

DESIGN dialogEmployeesConsignment {
    size = (800, 600);
    PROPERTY (lastName(e)) {
        charWidth = 15;
    }
    PROPERTY (firstName(e)) {
        charWidth = 25;
    }
    PROPERTY (namePosition(e)) {
        charWidth = 30;
    }
    PROPERTY (nameMainRole(e)) {
        charWidth = 25;
    }
}

// ------------------------------------- Отпуск товара --------------------------------- //
GROUP issuanceConsignment 'Отпуск' : base;

dataCustomerStock = ABSTRACT Stock (Consignment);
overCustomerStock = ABSTRACT Stock (Consignment);
customerStock (Consignment consignment)= 
    OVERRIDE overCustomerStock(consignment), dataCustomerStock(consignment) CHARWIDTH 20;
     
dataCustomer = ABSTRACT LegalEntity (Consignment);
overCustomer = ABSTRACT LegalEntity (Consignment);
customer (Consignment consignment)= OVERRIDE overCustomer(consignment), dataCustomer(consignment);  
    
overINNCustomer 'ИНН отправителя' = ABSTRACT STRING[50] (Consignment);
extraINNCustomer 'ИНН отправителя' = ABSTRACT STRING[50] (Consignment);
INNCustomer 'ИНН отправителя' (Consignment consignment) = OVERRIDE overINNCustomer(consignment), extraINNCustomer(consignment), inn(customer(consignment));

overKPPCustomer 'КПП отправителя' = ABSTRACT STRING[50] (Consignment);
extraKPPCustomer 'КПП отправителя' = ABSTRACT STRING[50] (Consignment);
KPPCustomer 'КПП отправителя' (Consignment consignment) = OVERRIDE overKPPCustomer(consignment), extraKPPCustomer(consignment), kpp(customer(consignment));

overOKPOCustomer 'ОКПО отправителя' = ABSTRACT STRING[50] (Consignment);
extraOKPOCustomer 'ОКПО отправителя' = ABSTRACT STRING[50] (Consignment);
OKPOCustomer 'ОКПО отправителя' (Consignment consignment) = OVERRIDE overOKPOCustomer(consignment), extraOKPOCustomer(consignment), okpo(customer(consignment));
    
overAddressCustomer = ABSTRACT ISTRING[150] (Consignment);
addressCustomer 'Юр. адрес отправителя' (Consignment consignment) =
    OVERRIDE overAddressCustomer(consignment), address(customer(consignment), date(consignment)) CHARWIDTH 30;      
    
overNameCustomerStock (Consignment consignment) = ABSTRACT ISTRING[150] (Consignment);
nameCustomerStock (Consignment consignment) =
     OVERRIDE overNameCustomerStock(consignment), name(customerStock(consignment)) CHARWIDTH 20;
      
overFullNameCustomer = ABSTRACT ISTRING[200] (Consignment);  
extraFullNameCustomer = ABSTRACT ISTRING[200] (Consignment);  
fullNameCustomer 'Наим-ие получателя' (Consignment consignment) = 
    OVERRIDE overFullNameCustomer(consignment), extraFullNameCustomer(consignment), fullName(customer(consignment)) CHARWIDTH 30;
    
overEmailCustomer = ABSTRACT STRING[400] (Consignment);    
emailCustomer 'E-mail' (Consignment consignment) = 
    OVERRIDE overEmailCustomer(consignment), email(customer(consignment)) CHARWIDTH 30;
    
overSiteCustomer = ABSTRACT STRING[100] (Consignment);    
siteCustomer 'Сайт'  (Consignment consignment) = 
    OVERRIDE overSiteCustomer(consignment), site(customer(consignment)) CHARWIDTH 30;    

overPhoneCustomer = ABSTRACT STRING[100] (Consignment);    
phoneCustomer 'Телефон/факс'  (Consignment consignment) = 
    OVERRIDE overPhoneCustomer(consignment), phone(customer(consignment)) CHARWIDTH 30;     
    
contract = ABSTRACT Contract (Consignment);

overDescriptionContract = ABSTRACT ISTRING[28] (Consignment);
descriptionContract 'Описание договора' (Consignment consignment)= 
    OVERRIDE overDescriptionContract(consignment), 'Договор поставки '+ description(contract(consignment));

overSeriesNumberContract = ABSTRACT ISTRING[28] (Consignment);
seriesNumberContract 'Серия/номер договора'(Consignment consignment)= 
    OVERRIDE overSeriesNumberContract(consignment), seriesNumber(contract(consignment));

overDateFromContract = ABSTRACT DATE (Consignment);
dateFromContract 'Серия/номер договора'(Consignment consignment)= 
    OVERRIDE overDateFromContract(consignment), dateFrom(contract(consignment));
    
notUseIssuanceExecuted 'Не использовать справочник сотрудников' = DATA BOOLEAN (Consignment);
dataIssuanceExecuted = DATA Employee(Consignment);
overIssuanceExecuted (consignment) = DATA Employee(Consignment);
issuanceExecuted (consignment) = ABSTRACT Employee(Consignment);
positionIssuanceExecuted 'Отпуск произвел, должность' (Consignment consignment) = 
    OVERRIDE namePosition(dataIssuanceExecuted(consignment)),
             namePosition(issuanceExecuted(consignment)),
             namePosition(overIssuanceExecuted(consignment)) IN issuanceConsignment CHARWIDTH 20;             
nameIssuanceExecuted 'Отпуск произвел, ФИО' (Consignment consignment) = 
    OVERRIDE firstShortName(dataIssuanceExecuted(consignment)),
             firstShortName(issuanceExecuted(consignment)),
             firstShortName(overIssuanceExecuted(consignment)) IN issuanceConsignment CHARWIDTH 20; 
overChangeIssuanceExecuted ABSTRACT (Consignment);

customSelectIssuanceExecuted = ABSTRACT BOOLEAN (Consignment);
overSelectIssuanceExecuted ABSTRACT (Consignment);

notUseDialogForwarder 'Не использовать справочник для принимающих сотрудников' = DATA BOOLEAN(Consignment) IN issuanceConsignment;

dataForwarder  = DATA Employee (Consignment);
overForwarder (consignment) = ABSTRACT Employee(Consignment);

dataNameForwarder = DATA STRING[100](Consignment);
overNameForwarder = ABSTRACT STRING[100](Consignment);
dataPositionForwarder = DATA STRING[100](Consignment);
overPositionForwarder = ABSTRACT STRING[100](Consignment);

forwarder 'Товар к перевозке принял (экспедитор)' (Consignment consignment) = 
    OVERRIDE  
        overForwarder(consignment), 
        dataForwarder(consignment) IN issuanceConsignment CHARWIDTH 30;

nameForwarder 'Товар к перевозке принял, ФИО' (Consignment consignment) = 
    OVERRIDE  
        IF NOT notUseDialogForwarder(consignment) THEN firstShortName(forwarder(consignment)),
        overNameForwarder(consignment),
        dataNameForwarder(consignment) IN issuanceConsignment CHARWIDTH 30;
    
positionForwarder 'Товар к перевозке принял, должность' (Consignment consignment) = 
    OVERRIDE  
        IF NOT notUseDialogForwarder(consignment) THEN namePosition(forwarder(consignment)),
        overPositionForwarder(consignment),
        dataPositionForwarder(consignment) IN issuanceConsignment CHARWIDTH 30;
        
warrantNumber 'По доверенности, номер' (consignment) = ABSTRACT STRING[30] (Consignment) IN issuanceConsignment CHARWIDTH 15;
warrantDate 'По доверенности, дата' (consignment) = ABSTRACT DATE (Consignment) IN issuanceConsignment CHARWIDTH 15;

//грузополучатель
notUseDialogGoodsAccepted 'Не использовать справочник для грузополучателя' = DATA BOOLEAN(Consignment) IN issuanceConsignment;

dataGoodsAccepted  = DATA Employee (Consignment);
overGoodsAccepted (consignment) = ABSTRACT Employee(Consignment);

dataNameGoodsAccepted = DATA STRING[100](Consignment);
overNameGoodsAccepted = ABSTRACT STRING[100](Consignment);
dataPositionGoodsAccepted = DATA STRING[100](Consignment);
overPositionGoodsAccepted = ABSTRACT STRING[100](Consignment);

goodsAccepted 'Принял грузополучатель' (Consignment consignment) = 
    OVERRIDE  
        overGoodsAccepted(consignment), 
        dataGoodsAccepted(consignment) IN issuanceConsignment CHARWIDTH 30;

nameGoodsAccepted 'Принял грузополучатель, ФИО' (Consignment consignment) = 
    OVERRIDE  
        IF NOT notUseDialogGoodsAccepted(consignment) THEN firstShortName(goodsAccepted(consignment)),
        overNameGoodsAccepted(consignment),
        dataNameGoodsAccepted(consignment) IN issuanceConsignment CHARWIDTH 30;
    
positionGoodsAccepted 'Принял грузополучатель, должность' (Consignment consignment) = 
    OVERRIDE  
        IF NOT notUseDialogGoodsAccepted(consignment) THEN namePosition(goodsAccepted(consignment)),
        overPositionGoodsAccepted(consignment),
        dataPositionGoodsAccepted(consignment) IN issuanceConsignment CHARWIDTH 30;
     
    
banSeries = ABSTRACT BOOLEAN (Consignment);
toShowSeries = Consignment c IS Consignment AND NOT banSeries(c);

dataCountPages 'Кол-во листов в приложении' (consignment) = DATA INTEGER (Consignment);
overCountPages 'Кол-во листов в приложении' (consignment) = ABSTRACT INTEGER (Consignment);
countPages 'Кол-во листов в приложении' (Consignment consignment) = OVERRIDE overCountPages(consignment), dataCountPages(consignment);

managerCustomer 'Руководитель' = ABSTRACT ISTRING[250] (Consignment);
accountantCustomer 'Главный бухгалтер' = ABSTRACT ISTRING[250] (Consignment);

consignment (d) = ABSTRACT Consignment (ConsignmentDetail);

order 'Порядок сортировки' = ABSTRACT ISTRING[255] (ConsignmentDetail); 
index 'Номер строки' = ABSTRACT INTEGER (ConsignmentDetail); 
skip = ABSTRACT BOOLEAN (ConsignmentDetail);

batch = ABSTRACT Batch (ConsignmentDetail);
nameBatch 'Партия' (ConsignmentDetail d) = name(batch(d)) CHARWIDTH 20;

dataSku (d) = ABSTRACT Sku (ConsignmentDetail);
overSku (d) = ABSTRACT Sku (ConsignmentDetail);
sku (ConsignmentDetail d) = OVERRIDE overSku(d), dataSku(d);
idSku (ConsignmentDetail d) = id(sku(d));

overNameSku = ABSTRACT ISTRING[255](ConsignmentDetail); // дополнительные характеристики
dataNameSku = ABSTRACT ISTRING[255](ConsignmentDetail);
nameSku 'Наименование товара' (ConsignmentDetail d) = CONCAT ' ',
    (OVERRIDE overDocumentNameSku(batch(d)), dataNameSku(d), name(sku(d))),
    overNameSku(d) CHARWIDTH 30;
    
dataQuantity 'Количество' (d) = ABSTRACT NUMERIC[16,5] (ConsignmentDetail);
overQuantity 'Количество' (d) = ABSTRACT NUMERIC[16,5] (ConsignmentDetail);
quantity 'Количество' (ConsignmentDetail d) = OVERRIDE overQuantity(d), dataQuantity(d);    

dataAmountPack 'Кол-во в упаковке' (d) = ABSTRACT NUMERIC[21,6] (ConsignmentDetail);
overAmountPack 'Кол-во в упаковке' (d) = ABSTRACT CASE NUMERIC[21,6] (ConsignmentDetail);
amountPack 'Кол-во в упаковке' (ConsignmentDetail d) = OVERRIDE overAmountPack(d), dataAmountPack(d);

dataPackQuantity 'Количество грузовых мест' (d) = ABSTRACT NUMERIC[21,6] (ConsignmentDetail);
overPackQuantity 'Количество грузовых мест' (d) = ABSTRACT CASE NUMERIC[21,6] (ConsignmentDetail);
packQuantity 'Количество грузовых мест' (ConsignmentDetail d) = OVERRIDE overPackQuantity(d), dataPackQuantity(d);

nameTransportPack 'Количество грузовых мест' (ConsignmentDetail d) = name(transportPack(barcode(sku(d))));

dataGrossWeight 'Масса брутто, кг.' (d) = ABSTRACT NUMERIC[16,5] (ConsignmentDetail);
overGrossWeight 'Масса брутто, кг.' (d) = ABSTRACT NUMERIC[16,5] (ConsignmentDetail);
grossWeight 'Масса брутто, кг.' (ConsignmentDetail d) = OVERRIDE overGrossWeight(d), dataGrossWeight(d);

dataNetWeight 'Масса нетто, кг.' (d) = ABSTRACT NUMERIC[16,5] (ConsignmentDetail);
overNetWeight 'Масса нетто, кг.' (d) = ABSTRACT NUMERIC[16,5] (ConsignmentDetail);
netWeight 'Масса нетто, кг.' (ConsignmentDetail d) = OVERRIDE overNetWeight(d), dataNetWeight(d);

dataShipmentQuantity 'Количество (посталено)' (d) = ABSTRACT NUMERIC[16,5] (ConsignmentDetail);
overShipmentQuantity 'Количество (посталено)' (d) = ABSTRACT NUMERIC[16,5] (ConsignmentDetail);
shipmentQuantity 'Количество (посталено)' (ConsignmentDetail d) = OVERRIDE overShipmentQuantity(d), dataShipmentQuantity(d);

dataShipmentGrossWeight 'Масса брутто (посталено), кг.' (d) = ABSTRACT NUMERIC[27,10] (ConsignmentDetail);
overShipmentGrossWeight 'Масса брутто (посталено), кг.' (d) = ABSTRACT NUMERIC[27,10] (ConsignmentDetail);
shipmentGrossWeight 'Масса брутто (посталено), кг.' (ConsignmentDetail d) = OVERRIDE overGrossWeight(d), dataGrossWeight(d);

dataShipmentNetWeight 'Масса нетто (посталено), кг.' (d) = ABSTRACT NUMERIC[27,10] (ConsignmentDetail);
overShipmentNetWeight 'Масса нетто (посталено), кг.' (d) = ABSTRACT NUMERIC[27,10] (ConsignmentDetail);
shipmentNetWeight 'Масса нетто (посталено), кг.' (ConsignmentDetail d) = OVERRIDE overNetWeight(d), dataNetWeight(d);

barCode 'Штрихкод' (ConsignmentDetail d) = idBarcode(sku(d));
sidCountry 'Код страны' (ConsignmentDetail d) = sid(country(sku(d)));
shortNameCountry 'Краткое наименование страны' (ConsignmentDetail d) = name(country(sku(d)));

shortNameUOMSku (ConsignmentDetail d) = shortNameUOM(sku(d));
overNameUOM = ABSTRACT ISTRING[255](ConsignmentDetail);    
shortNameUOM 'Единица измерения' (ConsignmentDetail d) = OVERRIDE overNameUOM(d), shortNameUOM(sku(d)) CHARWIDTH 5;

idUOMSku (ConsignmentDetail d) = idUOM(sku(d));
overIdUOM = ABSTRACT STRING[100] (ConsignmentDetail) CHARWIDTH 10;
idUOM 'Код ед. изм.' (ConsignmentDetail d) = OVERRIDE overIdUOM(d), idUOMSku(d) CHARWIDTH 10;

dataPrice 'Цена' (d) = ABSTRACT CASE NUMERIC[16,4] (ConsignmentDetail);
overPrice 'Цена' (d) = ABSTRACT CASE NUMERIC[16,4] (ConsignmentDetail);
price 'Цена' (ConsignmentDetail d) = OVERRIDE overPrice(d), dataPrice(d);

dataSum 'Сумма без НДС' (d) = ABSTRACT CASE NUMERIC[18,4] (ConsignmentDetail);
overSum 'Сумма без НДС' (d) = ABSTRACT CASE NUMERIC[18,4] (ConsignmentDetail);
sum 'Сумма без НДС' (ConsignmentDetail d) = OVERRIDE overSum(d), dataSum(d);

dataShipmentPrice 'Учетная цена' (d) = ABSTRACT CASE NUMERIC[16,4] (ConsignmentDetail);
overShipmentPrice 'Учетная цена' (d) = ABSTRACT CASE NUMERIC[16,4] (ConsignmentDetail);
shipmentPrice 'Учетная цена' (ConsignmentDetail d) = OVERRIDE overShipmentPrice(d), dataShipmentPrice(d);

dataShipmentSum 'Учетная сумма' (d) = ABSTRACT CASE NUMERIC[18,4] (ConsignmentDetail);
overShipmentSum 'Учетная сумма' (d) = ABSTRACT CASE NUMERIC[18,4] (ConsignmentDetail);
shipmentSum 'Учетная сумма' (ConsignmentDetail d)= OVERRIDE overShipmentSum(d), dataShipmentSum(d);

dataVAT 'НДС, %' (d) = ABSTRACT CASE NUMERIC[10,5] (ConsignmentDetail);
vat 'НДС, %' (ConsignmentDetail d) = dataVAT(d);

dataSumVAT 'Сумма НДС' (d) = ABSTRACT CASE NUMERIC[18,4] (ConsignmentDetail);
sumVAT 'Сумма НДС' (ConsignmentDetail d) = dataSumVAT(d);

dataSumInvoice 'Сумма с НДС' (d) = ABSTRACT CASE NUMERIC[18,4] (ConsignmentDetail);
sumInvoice 'Сумма с НДС' (ConsignmentDetail d) = dataSumInvoice(d);

deviationQuantity 'Отклонение количества' (ConsignmentDetail d) = quantity(d) - shipmentQuantity(d);
deviationGrossWeight 'Отклонение массы брутто' (ConsignmentDetail d) = grossWeight(d) - shipmentGrossWeight(d);
deviationNetWeight 'Отклонение массы нетто' (ConsignmentDetail d) = netWeight(d) - shipmentNetWeight(d);
deviationSum 'Отклонение стоимости' (ConsignmentDetail d) = sum(d) - shipmentSum(d);

overplusQuantity 'Излишек, количество' (ConsignmentDetail d) = - deviationQuantity(d);
overplusSum 'Излишек, сумма' (ConsignmentDetail d) = - deviationSum(d);

nameSort 'Сорт' (ConsignmentDetail d) = nameSort(sku(d));

banPrint 'Запрет печати' = ABSTRACT CASE BOOLEAN (Consignment);

// Заказ
dataOrderDetail = ABSTRACT Purchase.OrderDetail (ConsignmentDetail);
overOrderDetail = ABSTRACT Purchase.OrderDetail (ConsignmentDetail);
orderDetail (ConsignmentDetail d)= OVERRIDE overOrderDetail(d), dataOrderDetail(d);

overSeriesNumberOreder 'Серия/номер заказа' (Consignment consignment) = ABSTRACT STRING[200] (Consignment);
seriesNumberOreder (Consignment consignment) = OVERRIDE overSeriesNumberOreder(consignment),
    GROUP CONCAT seriesNumber(orderDetail(ConsignmentDetail d)) IF [GROUP LAST ConsignmentDetail d2 BY d2](d) AND consignment == consignment(d), ' ';

overDateFromOreder = ABSTRACT DATE (Consignment);
dateOreder'Дата заказа'(Consignment consignment)= OVERRIDE overDateFromContract(consignment), 
    GROUP LAST date(orderDetail(ConsignmentDetail d)) IF consignment == consignment(d);

// Автомобиль
GROUP carConsignment 'Автомобиль' : base;

notUseDriverTruck 'Не использовать справочники для выбора водителей и машин' = ABSTRACT BOOLEAN(Consignment);

dataTruck  = DATA Truck (Consignment);
overTruck (consignment) = ABSTRACT Truck(Consignment);
truck(Consignment consignment) = OVERRIDE overTruck(consignment), dataTruck(consignment); 

dataSidTruck = DATA STRING[100](Consignment);
overSidTruck = ABSTRACT STRING[100](Consignment);

sidTruck 'Номер автомобиля' (Consignment consignment) = 
    OVERRIDE  
        IF NOT notUseDriverTruck(consignment) THEN sid(truck(consignment)),
        overSidTruck(consignment),
        dataSidTruck(consignment) IN issuanceConsignment CHARWIDTH 30;
        
// Комиссия в атрибутах накладной
CLASS ConsignmentCommittee 'Комиссия для накладной' : Committee;

GROUP consignmentCommittee 'Комиссия' : public;

name 'Наименование' = DATA ISTRING[150](ConsignmentCommittee);
name(ConsignmentCommittee committee) += name(committee) IF committee IS ConsignmentCommittee;

committee(consignment) = DATA ConsignmentCommittee(Consignment);
nameCommittee 'Название комиссии' (Consignment consignment) = name(committee(consignment)) IN consignmentCommittee CHARWIDTH 30;
shortNameChairman 'Председатель комиссии' (Consignment consignment) = shortNameChairman(committee(consignment)) IN consignmentCommittee CHARWIDTH 30;
namePositionChairman 'Должность председателя' (Consignment consignment)  = namePositionChairman(committee(consignment)) IN consignmentCommittee CHARWIDTH 30;
nameEmployee 'Члены комиссии' (Consignment consignment) = nameEmployee(committee(consignment)) IN consignmentCommittee CHARWIDTH 30;
shortNamePositionEmployee (Consignment consignment) = shortNamePositionEmployee(committee(consignment)) IN consignmentCommittee CHARWIDTH 30;
in 'Является членом комиссии' (Consignment consignment, Employee employee) = in(committee(consignment), employee);

opinionCommittee 'Заключение комиссии'  (Consignment consignment) = ABSTRACT TEXT (Consignment);
decisionChief 'Решение руководителя' (Consignment consignment) = ABSTRACT TEXT (Consignment);

changeIssuanceExecuted(Consignment consignment) {    
    IF notUseIssuanceExecuted(consignment) THEN {
        INPUT s = nameIssuanceExecuted(consignment) DO {
            nameIssuanceExecuted(consignment) <- s;
        } 
        INPUT s = positionIssuanceExecuted(consignment) DO {
            positionIssuanceExecuted(consignment) <- s;
        } 
                
    } ELSE 
        IF customSelectIssuanceExecuted(consignment) THEN {
            overSelectIssuanceExecuted(consignment);
        } ELSE {
            DIALOG dialogEmployeesConsignment OBJECTS e = issuanceExecuted(consignment) INPUT NULL DO {
                issuanceExecuted(consignment) <- e;
                IF NOT issuanceExecuted(consignment) THEN {
                    overIssuanceExecuted(consignment) <- e;
                }
            }               
        }        
    overChangeIssuanceExecuted(consignment);
}

changeNameForwarder(Consignment consignment) {     
    IF notUseDialogForwarder(consignment) THEN {
        INPUT s = dataNameForwarder(consignment) DO {
            dataNameForwarder(consignment) <- s;
        }
    } ELSE {
        DIALOG dialogEmployeesConsignment OBJECTS e = dataForwarder(consignment) INPUT NULL DO {
            dataForwarder(consignment) <- e;
        }
    }
}   

changePositionForwarder(Consignment consignment)  {     
    IF notUseDialogForwarder(consignment) THEN {
        INPUT s = dataPositionForwarder(consignment) DO {
            dataPositionForwarder(consignment) <- s;
        }         
    } ELSE {
        DIALOG dialogEmployeesConsignment OBJECTS e = dataForwarder(consignment) INPUT NULL DO {
            dataForwarder(consignment) <- e;
        }
    }        
} 
           
changeNameGoodsAccepted(Consignment consignment) {     
    IF notUseDialogGoodsAccepted(consignment) THEN {
        INPUT s = dataNameGoodsAccepted(consignment) DO {
            dataNameGoodsAccepted(consignment) <- s;
        }
    } ELSE {
        DIALOG dialogEmployeesConsignment OBJECTS e = dataGoodsAccepted(consignment) INPUT NULL DO {
            dataGoodsAccepted(consignment) <- e;
        }
    }
}   

changePositionGoodsAccepted(Consignment consignment) {     
    IF notUseDialogGoodsAccepted(consignment) THEN {
        INPUT s = dataPositionGoodsAccepted(consignment) DO {
            dataPositionGoodsAccepted(consignment) <- s;
        }         
    } ELSE {
        DIALOG dialogEmployeesConsignment OBJECTS e = dataGoodsAccepted(consignment) INPUT NULL DO {
            dataGoodsAccepted(consignment) <- e;
        }
    }        
}      

overChangeTruck  ABSTRACT (Consignment); 

changeTruck (Consignment consignment)  {    
    IF notUseDriverTruck(consignment) THEN {
        INPUT s = dataSidTruck(consignment) DO {
            dataSidTruck(consignment) <- s;
        }
    } ELSE {        
        DIALOG trucks OBJECTS t = dataTruck(consignment) INPUT NULL DO {
            dataTruck(consignment) <- t;
        }
    }        
    overChangeTruck(consignment);
}
   
FORM consignmentTorg13 'ТОРГ-13'
    OBJECTS c=Consignment PANEL

    PROPERTIES (c) date, number, 
                   fullNameSupplier, addressSupplier, emailSupplier, siteSupplier, phoneSupplier, OKPOSupplier,
                   nameSupplierStock, nameCustomerStock, 
                   nameIssuanceExecuted,
                   positionIssuanceExecuted, 
                   nameForwarder,
                   positionForwarder                   
                   
    OBJECTS d=ConsignmentDetail
    
    PROPERTIES(d) shortNameUOMSku, nameSku, 
                  quantity, barCode, shortNameUOM, idUOM, 
                  grossWeight, netWeight, 
                  shipmentPrice, shipmentSum
        
    FILTERS consignment(d) == c,
            quantity(d),
            NOT skip(d)
;

printConsignmentTorg13 'ТОРГ-13' (Consignment consignment) { PRINT consignmentTorg13 OBJECTS c = consignment;} IMAGE 'print.png' IN print;

FORM consignmentTorg12 'ТОРГ-12'
    OBJECTS c=Consignment PANEL

    PROPERTIES (c) fullNameSupplier, addressSupplier, emailSupplier, siteSupplier, phoneSupplier, OKPOSupplier, INNSupplier,
                   fullNameCustomer, addressCustomer, emailCustomer, siteCustomer, phoneCustomer, OKPOCustomer, INNCustomer,
                   nameSupplierStock,
                   date, number,
                   nameCustomerStock, 
                   descriptionContract,
                   nameIssuanceExecuted, positionIssuanceExecuted, 
                   nameForwarder, positionForwarder
    OBJECTS d=ConsignmentDetail
    
    PROPERTIES(d) nameSku, barCode, shortNameUOM, idUOM,  
                  quantity, 
                  nameTransportPack, amountPack, packQuantity, grossWeight, 
                  price, sum, vat, sumVAT, sumInvoice
        
    FILTERS consignment(d) == c,
            quantity(d),
            NOT skip(d)
;
 
printConsignmentTorg12 'ТОРГ-12' (Consignment consignment) { PRINT consignmentTorg12 OBJECTS c = consignment;} IMAGE 'print.png' IN print;    

FORM consignmentUPD 'Универсальный передаточный документ'
    OBJECTS c=Consignment PANEL

    PROPERTIES (c) fullNameSupplier, addressSupplier, INNSupplier, KPPSupplier,
                   fullNameCustomer, addressCustomer, INNCustomer, KPPCustomer,
                   date, number,
                   descriptionContract,
                   nameIssuanceExecuted, positionIssuanceExecuted, 
                   nameForwarder, positionForwarder
    OBJECTS d=ConsignmentDetail
    
    PROPERTIES(d) nameSku, barCode, shortNameUOM, idUOM,  
                  quantity, 
                  grossWeight, 
                  price, sum, vat, sumVAT, sumInvoice,
                  sidCountry, shortNameCountry
        
    FILTERS consignment(d) == c,
            quantity(d),
            NOT skip(d)
;
 
printConsignmentUPD 'Универсальный передаточный документ' (Consignment consignment) { PRINT consignmentUPD OBJECTS c = consignment;} IMAGE 'print.png' IN print;    

FORM consignmentTorg1 'ТОРГ-1'
    // 1 страница
    OBJECTS ct=Consignment PANEL
    PROPERTIES (ct) fullNameCustomer, addressCustomer, phoneCustomer, OKPOCustomer,
                    fullNameSupplier, addressSupplier, phoneSupplier, OKPOSupplier,
                    nameCustomerStock, 
                    nameSupplierStock,
                    date, number,
                    descriptionContract,
                    sidTruck,
                    seriesNumberOreder, dateOreder, seriesNumberContract, dateFromContract
    
    // 2 страница
    OBJECTS cd=Consignment
    OBJECTS d=ConsignmentDetail
    
    PROPERTIES(d) nameSku, barCode, shortNameUOM, idUOM,  
                  quantity, grossWeight, netWeight,
                  price, sum
        
    FILTERS consignment(d) == cd,
            quantity(d),
            NOT skip(d)
        
    // 3 страница
    OBJECTS cd2=Consignment    
    OBJECTS d2=ConsignmentDetail
        PROPERTIES(d2) nameSku, barCode, shortNameUOM, idUOM, 
                       shipmentQuantity, shipmentGrossWeight, shipmentNetWeight, shipmentPrice, shipmentSum,
                       sumInvoice, vat, sumVAT,
                       deviationQuantity, deviationGrossWeight, deviationNetWeight, deviationSum 
                      
            
    FILTERS consignment(d2) == cd2,
            quantity(d2),
            NOT skip(d2)        
                        
    // 4 страница                    
    OBJECTS ct2=Consignment     
    PROPERTIES (ct2)  shortNameChairman, namePositionChairman,
                      nameIssuanceExecuted, positionIssuanceExecuted, 
                      nameForwarder, positionForwarder, 
                      warrantNumber, warrantDate,
                      nameGoodsAccepted, positionGoodsAccepted,                     
                      opinionCommittee, decisionChief,
                      countPages
    
    OBJECTS  e = Employee
    PROPERTIES (e) shortName, namePosition
    
    FILTERS in(committee(ct2), e)
;
 
printConsignmentTorg1 'ТОРГ-1' (Consignment consignment) { 
    PRINT consignmentTorg1 OBJECTS ct = consignment, cd = consignment, cd2 = consignment, ct2 = consignment;
} IMAGE 'print.png' IN print;    


FORM consignmentTorg2 'ТОРГ-2'
    // 1 страница
    OBJECTS ct=Consignment PANEL
    PROPERTIES (ct) fullNameCustomer, addressCustomer, phoneCustomer, OKPOCustomer,
                    fullNameSupplier, addressSupplier, phoneSupplier, OKPOSupplier,
                    nameCustomerStock, 
                    nameSupplierStock,
                    date, number,
                    descriptionContract,
                    sidTruck,
                    seriesNumberOreder, dateOreder, seriesNumberContract, dateFromContract
    
    // 2 страница
    OBJECTS cd=Consignment
    OBJECTS d=ConsignmentDetail
    
    PROPERTIES(d) nameSku, shortNameUOM, idUOM, nameSort,  
                  quantity,
                  price, sum
        
    FILTERS consignment(d) == cd,
            quantity(d),
            NOT skip(d)
        
    // 3 страница
    OBJECTS cd2=Consignment    
    OBJECTS d2=ConsignmentDetail
        PROPERTIES(d2) nameSort, shipmentQuantity,shipmentPrice, shipmentSum,
                       sumInvoice, vat, sumVAT,
                       deviationQuantity, deviationSum, 
                       overplusQuantity, overplusSum  
                                  
    FILTERS consignment(d2) == cd2,
            quantity(d2),
            NOT skip(d2)        
                        
    // 4 страница                    
    OBJECTS ct2=Consignment     
    PROPERTIES (ct2)  shortNameChairman, namePositionChairman,
                      nameIssuanceExecuted, positionIssuanceExecuted, 
                      nameForwarder, positionForwarder, 
                      warrantNumber, warrantDate,
                      nameGoodsAccepted, positionGoodsAccepted,                     
                      opinionCommittee, decisionChief,
                      countPages
    
    OBJECTS  e = Employee
    PROPERTIES (e) shortName, namePosition
    
    FILTERS in(committee(ct2), e)
;

printConsignmentTorg2 'ТОРГ-2' (Consignment consignment) { 
    PRINT consignmentTorg2 OBJECTS ct = consignment, cd = consignment, cd2 = consignment, ct2 = consignment;
} IMAGE 'print.png' IN print; 

FORM consignmentTorg3 'ТОРГ-3'
        // 2 страница
        OBJECTS cd=Consignment 
        OBJECTS d=ConsignmentDetail
        
        PROPERTIES(d) nameSku, shortNameUOM, idUOM, nameSort,  
                      quantity,
                      price, sum, 
                      shipmentQuantity, 
                      deviationQuantity, deviationSum, 
                      overplusQuantity, overplusSum,
                      barCode
            
        FILTERS consignment(d) == cd,
                quantity(d),
                NOT skip(d)            
                            
        // 4 страница                    
        OBJECTS ct2=Consignment     
        PROPERTIES (ct2)  shortNameChairman, namePositionChairman,
                          nameIssuanceExecuted, positionIssuanceExecuted, 
                          nameForwarder, positionForwarder, 
                          warrantNumber, warrantDate,
                          nameGoodsAccepted, positionGoodsAccepted,                     
                          opinionCommittee, decisionChief,
                          countPages
        
        OBJECTS  e = Employee
        PROPERTIES (e) shortName, namePosition
        
        FILTERS in(committee(ct2), e)
;

FORM consignmentTorg3FirstPage 'ТОРГ-3'
    // 1 страница
        OBJECTS ct=Consignment PANEL
        PROPERTIES (ct) fullNameCustomer, addressCustomer, phoneCustomer, OKPOCustomer,
                        fullNameSupplier, addressSupplier, phoneSupplier, OKPOSupplier,
                        nameCustomerStock, 
                        nameSupplierStock,
                        date, number,
                        descriptionContract,
                        sidTruck,
                        seriesNumberOreder, dateOreder, seriesNumberContract, dateFromContract       
;
printConsignmentTorg3 'ТОРГ-3' (Consignment consignment) { 
    PRINT consignmentTorg3FirstPage OBJECTS ct = consignment;
    PRINT consignmentTorg3 OBJECTS cd = consignment, ct2 = consignment;    
} IMAGE 'print.png' IN print; 

FORM consignmentCommittee 'Комиссия для накладной'
    OBJECTS c=ConsignmentCommittee PANEL
    PROPERTIES(c) name, nameChairman

    TREE stockTree sg = StockGroup PARENT parent(sg)
    PROPERTIES READONLY sgTreeName = name(sg)

    OBJECTS ts=Stock
    PROPERTIES READONLY tsTreeName = name(ts)
    PROPERTIES(c, ts) in 
    FILTERGROUP inactiveStock FILTER 'Активный' active(ts) 'ctrl F10' DEFAULT
    FILTERS isParent(sg, ts)
    ORDERS tsTreeName

    OBJECTS e=Employee
    PROPERTIES(e) READONLY name[Contact], firstName, lastName, namePosition
    PROPERTIES(e) NEWEDIT, EDIT, DELETE GRID

    PROPERTIES(c, e) in
    FILTERS (countStock (e, c) AND NOT allowStockEmployee()) OR (e IS Employee AND allowStockEmployee())
    FILTERGROUP filters6
        FILTER 'Показывать только членов комиссии' in(c, e) 'F10'

    FILTERGROUP filters5
        FILTER 'Показывать отделы только для данной комиссии' in(c, ts) 'F9'
    FILTERGROUP active FILTER 'Активные' active(e) 'F5' DEFAULT 
    EDIT ConsignmentCommittee OBJECT c
;
@extendFormFilterStockAccess(ts, consignmentCommittee);
@extendFormFilterStockGroupAccess(sg, consignmentCommittee);

DESIGN consignmentCommittee {
    BOX {
        size = (1024, 768);

        OBJECTS {
            NEW caseOne BEFORE BOX(e) {
                fill = 1;
                type = SPLITH;
    
                MOVE BOX(TREE stockTree) {
                    caption = 'Группы складов';
                }
                MOVE BOX(ts) {
                    fill = 1;
                }
            }
        }
    }
}

FORM consignmentCommittees 'Комиссии для накладной'
    OBJECTS w=ConsignmentCommittee
    PROPERTIES(w) READONLY name, nameStocks, nameEmployee, nameChairman
    PROPERTIES(w) NEWSESSION NEW, EDIT, DELETE

    LIST ConsignmentCommittee OBJECT w
;

FORM consignment 'Атрибуты накладной'
    OBJECTS c=Consignment PANEL

    PROPERTIES(c) READONLY nameSupplierStock
    PROPERTIES(c)  number, series SHOWIF toShowSeries(c), date, 
                   addressSupplier READONLY, addressCustomer READONLY, fullNameSupplier READONLY, fullNameCustomer READONLY, 
                   notUseDialogForwarder,
                   nameIssuanceExecuted ON CHANGE changeIssuanceExecuted(c),
                   positionIssuanceExecuted ON CHANGE changeIssuanceExecuted(c), 
                   nameForwarder ON CHANGE changeNameForwarder(c),
                   positionForwarder ON CHANGE changePositionForwarder(c),                   
                   notUseDialogGoodsAccepted,
                   nameGoodsAccepted ON CHANGE changeNameGoodsAccepted(c),
                   positionGoodsAccepted ON CHANGE changePositionGoodsAccepted(c),
                   sidTruck ON CHANGE changeTruck(c),
                   nameCommittee, opinionCommittee, 
                   warrantNumber, warrantDate,
                   decisionChief, 
                   countPages
    OBJECTS d=ConsignmentDetail

    PROPERTIES(d) order, index, nameSku READONLY, shortNameUOM READONLY, quantity, price, sum 
    ORDERS order(d), index(d)  

    FILTERS consignment(d) == c, 
            NOT skip(d) 
;

DESIGN consignment {
    size = (1024, 768);
    NEW pane {
        fill = 1;
        type = TABBED;
        NEW mainContainer {
            caption = 'Основная информация';
            NEW mainScroll {
                fill = 1;
                height = 1;
                type = SCROLL;
                NEW mainTab {
                    alignment = STRETCH;
                    NEW header {
                        alignment = STRETCH;
                        caption = 'Шапка документа';
                        type = CONTAINERH;
                        MOVE PROPERTY(nameSupplierStock(c));
                        MOVE PROPERTY(number(c));
                        MOVE PROPERTY(series(c));
                        MOVE PROPERTY(date(c));
                        MOVE PROPERTY(nameCommittee(c));
                    }
                
                    NEW supplier {
                        alignment = STRETCH;
                        caption = 'Грузоотправитель';
                        type = CONTAINERH;
                        MOVE PROPERTY(fullNameSupplier(c));
                        MOVE PROPERTY(addressSupplier(c));
                    }
                
                    NEW customer {
                        alignment = STRETCH;
                        caption = 'Грузополучатель';
                        type = CONTAINERH;
                        MOVE PROPERTY(fullNameCustomer(c));
                        MOVE PROPERTY(addressCustomer(c));
                    } 
                    
                    MOVE GROUP(issuanceConsignment,c) {                       
                        type = CONTAINERV;
                        
                        NEW issuance { 
                            alignment = STRETCH;
                            type = CONTAINERH;
                            MOVE PROPERTY(nameIssuanceExecuted(c)) { charWidth = 30; };
                            MOVE PROPERTY(positionIssuanceExecuted(c)) { charWidth = 20; };
                        }    
                            
                        MOVE PROPERTY(notUseDialogForwarder(c));    
                        
                        NEW forwarder { 
                            alignment = STRETCH;
                            type = CONTAINERH;
                            MOVE PROPERTY(nameForwarder(c)) { charWidth = 30; };
                            MOVE PROPERTY(positionForwarder(c)) { charWidth = 20; };                              
                        }         
                        
                        NEW warrant {
                            alignment = STRETCH;
                            type = CONTAINERH;
                            MOVE PROPERTY(warrantNumber(c));
                            MOVE PROPERTY(warrantDate(c));
                        }
                        
                        MOVE PROPERTY(notUseDialogGoodsAccepted(c)); 
                        
                        NEW goodsAccepted { 
                            alignment = STRETCH;
                            type = CONTAINERH;
                            MOVE PROPERTY(nameGoodsAccepted(c)) { charWidth = 30; };
                            MOVE PROPERTY(positionGoodsAccepted(c)) { charWidth = 20; };                              
                        }    
                        
                        MOVE PROPERTY(sidTruck(c));
                    }   
                    
                    NEW committee {
                        caption = 'Комиссия';
                        type = CONTAINERV;                        
                        MOVE PROPERTY(nameCommittee(c));
                        MOVE PROPERTY(opinionCommittee(c)) { charWidth = 30; charHeight=2;};
                    }  
                    
                    NEW other {
                        caption = 'Дополнительные параметры';
                        type = CONTAINERV;
                        MOVE PROPERTY(countPages(c)) { charWidth = 30; }; 
                        MOVE PROPERTY(decisionChief(c)) { charWidth = 30; charHeight=2;};                                               
                    }                                         
                }
            }
        }
        
        MOVE BOX(d) {
            caption = 'Спецификация';
            PROPERTY(order(d)) {
                charWidth = 10;
            }
        };
    }
    
    MOVE TOOLBARBOX;
}

editAttributes 'Заполнить атрибуты накладной' (Consignment consignment) { 
	NEWSESSION {	       
	    SHOW consignment OBJECTS c = consignment DOCKED; 
	}
} IMAGE 'edit.png';

showAttributes 'Просмотреть атрибуты накладной' (Consignment consignment) { 
    NEWSESSION {
        SHOW consignment OBJECTS c = consignment DOCKED READONLY;
    }
}