MODULE GISMTPurchaseReturnShipment;

REQUIRE GISMTDocument, GISMTLegalEntity, PurchaseReturnShipmentLot;

PRIORITY GISMT, PurchaseReturn, Purchase;

NAMESPACE GISMTPurchaseReturn;

@defineOperationProperty(actionReceipt, 'Причина вывода из оборота', STRING, paramsContainer);

@defineOperationProperty(documentType, 'Вид первичного документа', STRING, paramsContainer);

@defineOperationProperty(primaryDocumentCustomName, 'Наименование первичного документа', STRING, paramsContainer);

needDocument = ABSTRACT VALUE BOOLEAN (Invoice);
needDocument(Invoice i) += supplier(i) != customer(i);
createDocument (Invoice i) = isPosted(i) AND createShipment(i) AND needDocument(i);
gismtType (Invoice i) = OVERRIDE 'LK_RECEIPT' IF actionReceipt(operation(i)), ('LP_SHIP_GOODS' IF i IS Invoice);
@defineDocumentGISMT(invoice, createDocument, invoices, i, customer, customerStock);

participant (InvoiceDocument i) += supplier(invoice(i));
participantStock (InvoiceDocument i) += supplierStock(invoice(i));

FORM exportInvoice
    OBJECTS i = Invoice, t = LotType PANEL
    
    PROPERTIES transfer_date = toDateISO(date(i)), 
               document_date = toDateISO(date(i)), 
               document_num = seriesNumber(i),
               turnover_type = 'SELLING',
               sender_inn = inn(customer(i)), 
               receiver_inn = inn(supplier(i))
    
    OBJECTS products = (d = InvoiceDetail, l = Lot)
    PROPERTIES uit_code = id(l) IF NOT aggregated(l),
               uitu_code = id(l) IF aggregated(l),
               product_description = nameSku(d)
    FILTERS invoice(d) = i, quantity(d, l), lotType(sku(d)) = t
;

FORM exportInvoiceReceipt
    OBJECTS i = Invoice, t = LotType PANEL
    
    PROPERTIES action = actionReceipt(operation(i)), action_date = toDateISO(date(i)), 
               document_date = toDateISO(date(i)), document_number = seriesNumber(i),
               document_type = documentType(operation(i)), primary_document_custom_name = primaryDocumentCustomName(operation(i)),
               inn = inn(customer(i))
    
    OBJECTS products = (d = InvoiceDetail, l = Lot)
    PROPERTIES cis = id(l)
    FILTERS invoice(d) = i, quantity(d, l), lotType(sku(d)) = t
;

export (InvoiceDocument i) + {
    IF actionReceipt(operation(invoice(i))) THEN
        EXPORT exportInvoiceReceipt OBJECTS i = invoice(i), t = lotType(i) JSON;
    ELSE
        EXPORT exportInvoice OBJECTS i = invoice(i), t = lotType(i) JSON;
}
