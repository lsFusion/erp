MODULE TerminalPreferences;

REQUIRE System, Terminal;

CLASS Preference 'Настройки' {
// nameGroup|order|jsonName|name|type
    GENERAL_DB_NAME						'GENERAL|01|DatabaseName|Файл БД|0|tsd',
    GENERAL_AUTODOCNUM					'GENERAL|02|AutoDocumentNumbering|Автоматическая нумерация документа|1',
    GENERAL_CONFIRM_OVERQUANTITY		'GENERAL|03|ConfirmOverQuantity|Подтверждать превышение количества при вводе документа|1',
    GENERAL_CONFIRM_LOAD_DB				'GENERAL|04|ConfirmLoadDB|Подтверждать загрузку БД при наличии невыгруженных документов|1',    
    GENERAL_DEFDOCVOP					'GENERAL|05|DefaultDocumentVop|Код операции для документа по умолчанию',
    GENERAL_POST_VAN					'GENERAL|06|PostVan|Вид аналитики поставщика|0|ПС',
    GENERAL_STOCK_VAN					'GENERAL|07|StockVan|Вид аналитики склада|0|СК',
    GENERAL_VOICE						'GENERAL|08|Voice|Голосовое сопровождение|1',    
    
    GENERAL_ALIAS_DATE1					'GENERAL|21|AliasDate1|Наименование поля ввода "Дата1"',
    GENERAL_ALIAS_DATE2					'GENERAL|22|AliasDate2|Наименование поля ввода "Дата2"',
    GENERAL_ALIAS_FIELD1				'GENERAL|23|AliasField1|Наименование поля ввода "Поле1"',
    GENERAL_ALIAS_FIELD2				'GENERAL|24|AliasField2|Наименование поля ввода "Поле2"',
    GENERAL_ALIAS_FIELD3				'GENERAL|25|AliasField3|Наименование поля ввода "Поле3"',
    GENERAL_ALIAS_EXTRAQUANT			'GENERAL|26|AliasExtraQuant|Наименование поля ввода "Доп. кол-во"',
    
    GENERAL_VESTEMPLATE					'GENERAL|08|VesTemplate|Шаблоны весовых штрихкодов|3',
        
    GENERAL_SHOW_MENU_IK                'GENERAL|27|ShowMenuIk|Пункт меню "Проверка цен"|1',
    GENERAL_SHOW_MENU_CHECKORDERPICKING 'GENERAL|28|ShowMenuCheckOrderPicking|Пункт меню "Проверка комплектации заказа"|1',
        
    ONLINE_ENABLE						'ONLINE|01|OnlineEnable|Включить On-line режим|1',
    ONLINE_SERVER						'ONLINE|01|OnlineServer|Сервер',
    ONLINE_PORT							'ONLINE|02|OnlinePort|Порт|2|2004',
    ONLINE_SEND_ORDER_STATU				'ONLINE|03|OnlineSendOrderStatus|Обмен статусами заказов|1',
    ONLINE_REQ_STOCK					'ONLINE|04|OnlineReqStock|Запрашивать склад при авторизации|1',
    ONLINE_TEST_CONNECT					'ONLINE|05|OnlineTestConnect|Мониторинг подключения к серверу|1',
    
    OBMEN_TYPE                      	'OBMEN|31|ObmenType|Формат файлового обмена|0|XML',
    OBMEN_DIR                       	'OBMEN|32|ObmenDir|Каталог обмена|0|smb://192.168.1.1/obmen',
    OBMEN_USER                      	'OBMEN|33|ObmenUserName|Пользователь',
    OBMEN_PASS                      	'OBMEN|34|ObmenPass|Пароль',
    OBMEN_DOMAIN                    	'OBMEN|35|ObmenDomain|Домен',
    OBMEN_FTP                       	'OBMEN|36|ObmenFtp|Включить FTP-обмен|1',
    OBMEN_FTP_SERVER                	'OBMEN|37|ObmenFtpServer|FTP-сервер',
    OBMEN_FTP_USER                  	'OBMEN|38|ObmenFtpUserName|FTP-логин',
    OBMEN_FTP_PASS                  	'OBMEN|39|ObmenFtpPass|FTP-пароль',
    OBMEN_FTP_DIR                   	'OBMEN|40|ObmenFtpDir|FTP-каталог обмена',
    
    UPDATEAPP_STARTUP					'UPD|51|UpdateAppStartUp|Проверять наличие обновления при старте приложения|1',
    UPDATEAPP_SERVER					'UPD|52|UpdateAppServer|FTP-сервер|0|office.luxsoft.by',
    UPDATEAPP_USER						'UPD|53|UpdateAppUser|Логин|0|tsd',
    UPDATEAPP_PASS						'UPD|54|UpdateAppPassword|Пароль|0|tsd',
    UPDATEAPP_DIR						'UPD|55|UpdateAppDir|Каталог'   
}

CLASS Obmen 'Формат обмена' {
    Obmen_SQLite '0|SQLite',
    Obmen_CSV '1|CSV',
    Obmen_XML '2|XML'
}

value (Obmen o) = STRING(getWord(staticCaption(o), '|',1));
name 'Формат' (Obmen o) = STRING(getWord(staticCaption(o), '|',2));
obmen (STRING s) = CASE 
    WHEN s == 'SQLite' THEN Obmen.Obmen_SQLite
    WHEN s == 'CSV' THEN Obmen.Obmen_CSV
    WHEN s == 'XML' THEN Obmen.Obmen_XML
;
        
FORM dialogObmen 'Формат обмена'
    OBJECTS o = Obmen
    PROPERTIES(o) READONLY name
    LIST Obmen OBJECT o
    ORDERS name(o);

CLASS PreferenceType {
    TYPE_STRING,
    TYPE_NUMERIC,
    TYPE_YESNO,
    TYPE_TEXT
}

CLASS YesNo {
    true 'true|Yes',
    false 'false|No'
}
value (YesNo y) = STRING(getWord(staticCaption(y), '|',1));
name (YesNo y) = STRING(getWord(staticCaption(y), '|',2));

nameYesNo(STRING s) = CASE 
    WHEN s = 'true' THEN name(YesNo.true)
    WHEN s = 'false' THEN name(YesNo.false) MATERIALIZED;

stringToYesNo(STRING s) = CASE 
    WHEN s = 'Yes' THEN YesNo.true
    WHEN s = 'No' THEN YesNo.false MATERIALIZED;


FORM dialogYesNo 'Значение'
    OBJECTS y = YesNo
    PROPERTIES name(y) READONLY
;

getType(STRING s) = CASE 
    WHEN s = '0' THEN PreferenceType.TYPE_STRING
    WHEN s = '1' THEN PreferenceType.TYPE_YESNO
    WHEN s = '2' THEN PreferenceType.TYPE_NUMERIC
    WHEN s = '3' THEN PreferenceType.TYPE_TEXT
    ELSE PreferenceType.TYPE_STRING;

enable 'Вкл' (Preference p) = DATA BOOLEAN;
nameGroup (Preference p) = STRING(getWord(staticCaption(p),'|', 1));
order (Preference p) = STRING(getWord(staticCaption(p),'|', 2));
namePreference (Preference p) = STRING(getWord(staticCaption(p),'|', 3));
name 'Параметр' (Preference p) = STRING(getWord(staticCaption(p),'|', 4));
defaultValue (Preference p) = STRING(getWord(staticCaption(p),'|', 6));
value = DATA STRING[100] (Preference);
type (Preference p) = getType(getWord(staticCaption(p),'|', 5));
overValue 'Значение' (Preference p) =
        OVERRIDE 'No' IF type(p) = PreferenceType.TYPE_YESNO AND NOT value(p),
        OVERRIDE defaultValue(p) IF NOT value(p), value(p);
foregroundLine (Preference p) = IF NOT enable(p) THEN RGB(169,169,169);

overNameGroup 'Группа' (Preference p) = CASE
    WHEN nameGroup(p) = 'GENERAL' THEN 'Основные настройки'
    WHEN nameGroup(p) = 'ONLINE' THEN 'On-line настройки'
    WHEN nameGroup(p) = 'OBMEN' THEN 'Обмен данными'
    WHEN nameGroup(p) = 'UPD' THEN 'Обновление'
    ELSE nameGroup(p);  

TABLE preferences (Preference);

FORM dialogText 'Текст'
    OBJECTS s = TEXT PANEL
    PROPERTIES(s) objValue = VALUE
;

DESIGN dialogText {
	BOX {
		size = (300, 150); 
        MOVE PROPERTY(objValue) { fill = 1; caption = ''; focusable = FALSE; font = 'bold 16'; }                  
	}
	MOVE TOOLBARBOX; 
}

changePreferenceValue(Preference p)  {
    CASE
        WHEN p == Preference.OBMEN_TYPE THEN {           
            DIALOG dialogObmen OBJECTS o = obmen(value(p)) INPUT DO {
                value(p) <- name(o);
            }
        }
        WHEN type(p) == PreferenceType.TYPE_TEXT THEN {
            DIALOG dialogText OBJECTS s = value(p) INPUT DO {
                value(p) <- s;
            }        	           
        }
        WHEN type(p) == PreferenceType.TYPE_STRING THEN {
            INPUT s = STRING DO {
                value(p) <- s;
            }
        }
        WHEN type(p) == PreferenceType.TYPE_NUMERIC THEN {
            INPUT s = NUMERIC DO {
                value(p) <- trim(TEXT(s));
            }
        }
        WHEN type(p) == PreferenceType.TYPE_YESNO THEN {           
            DIALOG dialogYesNo OBJECTS y = stringToYesNo(overValue(p)) INPUT DO {
                value(p) <- name(y);
            }
        }
}

FORM preferences 'Конфигурация ТСД'
    OBJECTS p = Preference    
    PROPERTIES(p) FOREGROUND foregroundLine(p) enable, overNameGroup, name, overValue ON CHANGE changePreferenceValue(p), order SHOWIF NULL
    ORDERS overNameGroup(p), order(p)
;

jsonValue(Preference p) = CASE
    WHEN p = Preference.OBMEN_TYPE THEN value(obmen(overValue(p))) 
    WHEN type(p) = PreferenceType.TYPE_YESNO THEN value(stringToYesNo(overValue(p)))
    WHEN type(p) = PreferenceType.TYPE_NUMERIC THEN overValue(p)
    ELSE '"' + overValue(p) + '"'
;

terminalPreferencesJSON = DATA LOCAL STRING();

getTerminalPreferences(terminalID) {

    LOCAL json = STRING();
    json() <- '';
    
    FOR Preference p IS Preference AND enable(p) DO {
        IF length(json()) > 0 THEN json() <- json() + ','; 
        json() <- json() + '"' + namePreference(p) + '":' + (OVERRIDE jsonValue(p), '');
    }
    IF length(json()) > 0 THEN json() <- '[\{' + json() + '\}]'; ELSE json() <- NULL;
    
    terminalPreferencesJSON() <- json();
}

NAVIGATOR {
    terminalNavigator {
        NEW preferences;
    }
}
